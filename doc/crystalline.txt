*crystalline.txt* Functions to help you make your own statusline/tabline

Author:  Roger Bongers <https://github.com/rbong>
License: Same as Vim (see |license|)

INTRODUCTION                                                     *crystalline*

vim-crystalline provides functions for creating custom statuslines and
tablines using Vim's native |'statusline'| syntax.

SETTINGS                                                *crystalline-settings*

Configure the following global variables before loading vim-crystalline.
Define global function settings with |:function|, not |function()|.

g:CrystallineStatuslineFn                          *g:CrystallineStatuslineFn*

Sets the statusline.
Accepts one argument, the |winnr()| for the statusline.

g:CrystallineTablineFn                                *g:CrystallineTablineFn*

Sets the tabline.

g:CrystallineTabFn                                        *g:CrystallineTabFn*

Custom tabs/buffers for |crystalline#tabs_or_buffers()|.

Arguments:
  `bufnr`
    The buffer number.
  `max_width`
    The max allowed width of the tab.
  `is_sel`
    Whether the tab is selected.

Return a list containing the tab content, the visible width of the tab, and
the number of |'statusline'| items used in the tab.

Defaults to |crystalline#default_tab()|.

g:CrystallineHideBufferFn                          *g:CrystallineHideBufferFn*

Hide buffers with |crystalline#tabs_or_buffers()|.

Arguments:
  `bufnr`
    The buffer number.

Returns a boolean (whether the buffer is hidden).

Defaults to |crystalline#default_hide_buffer()|.

g:crystalline_auto_prefix_mode_group    *g:crystalline_auto_prefix_mode_group*

Whether to automatically prefix all |crystalline-highlight-groups| with the
current mode when using |crystalline-functions|.

g:crystalline_group_suffix                        *g:crystalline_group_suffix*

A string to add to the end of to all |crystalline-highlight-groups| when using
|crystalline-functions|.

g:crystalline_theme                                      *g:crystalline_theme*

Automatically sets the theme.

g:crystalline_mode_labels                          *g:crystalline_mode_labels*

A dictionary containing labels to use for each |mode()|.

Default:
>vim
    let g:crystalline_mode_labels = {
          \ 'n': ' NORMAL ',
          \ 'c': ' COMMAND ',
          \ 'r': ' NORMAL ',
          \ '!': ' NORMAL ',
          \ 'i': ' INSERT ',
          \ 't': ' TERMINAL ',
          \ 'v': ' VISUAL ',
          \ 'V': ' VISUAL ',
          \ '': ' VISUAL ',
          \ 's': ' VISUAL ',
          \ 'S': ' VISUAL ',
          \ '': ' VISUAL ',
          \ 'R': ' REPLACE ',
          \ '': '',
          \ }
<

g:crystalline_separators                            *g:crystalline_separators*

Separators to use with |crystalline#sep()|.

This is a list of objects containing:
  `ch`
    The separator character.
  `alt_ch`
    The separator character to use between sections with the same background.
  `dir`
    The direction the separator faces (either ">" or "<").

Default:
>vim
    let g:crystalline_separators = [
          \ { 'ch': '', 'alt_ch': '', 'dir': '>' },
          \ { 'ch': '', 'alt_ch': '', 'dir': '<' },
          \ ]
<

FUNCTIONS                                              *crystalline-functions*

Documentation uses vimscript, but all functions can be called from Lua with:
>lua
    local crystalline = require("crystalline")
    crystalline.fn()
<

                                                       *crystalline.hi_item()*
crystalline#hi_item(group)                             *crystalline#hi_item()*

Create a statusline highlight group item.
Example:
>vim
    " Returns '%#CrystallineA#'
    let l:group = crystalline#hi_item('A')
<

See also |crystalline-highlight-groups|.

                                                    *crystalline.mode_group()*
crystalline#mode_group(group)                       *crystalline#mode_group()*

Prepend the current mode to the group.
Example:
>vim
    " Returns 'NormalModeA'
    let l:group = crystalline#mode_group('A')
<

You do not need to use this function if |g:crystalline_auto_prefix_mode_group|
is defined.

See also |crystalline-highlight-groups|.

                                                  *crystalline.mode_hi_item()*
crystalline#mode_hi_item(group)                   *crystalline#mode_hi_item()*

Create a statusline highlight group item with the current mode and the group.
Example:
>vim
    " Returns '%#CrystallineNormalModeA#'
    let l:group = crystalline#mode_hi_item('A')
<

See also |crystalline-highlight-groups|.

                                                    *crystalline.mode_label()*
crystalline#mode_label()                            *crystalline#mode_label()*

Get the current mode label. See also |g:crystalline_mode_labels|.

                                                  *crystalline.mode_section()*
                                                  *crystalline#mode_section()*
crystalline#mode_section(sep_index, left_group, right_group)

Create a statusline section containing the current mode and a separator.

Arguments:
  `sep_index`
    The index of a separator in |g:crystalline_separators| to use.
  `left_group`
    The left highlight group without the "Crystalline" prefix.
    When the separator is right facing, this is the mode group.
  `right_group`
    The right highlight group without the "Crystalline" prefix.
    When the separator is left facing, this is the mode group.

Equivalent to:
>vim
    " Right facing separator
    let l:section = crystalline#mode_hi_item(a:left_group)
          \ . crystalline#mode_label()
          \ . crystalline#sep(a:sep_index, crystalline#mode_sep_group(a:left_group), a:right_group)
    " Left facing separator
    let l:section = crystalline#mode_hi_item(a:mode_group)
          \ . crystalline#mode_label()
          \ . crystalline#sep(a:sep_index, a:left_group, crystalline#mode_sep_group(a:right_group))
<

See also |crystalline-highlight-groups|.

                                                           *crystalline.sep()*
crystalline#sep(sep_index, left_group, right_group)        *crystalline#sep()*

Create a separator between sections.

Arguments:
  `sep_index`
    The index of the separaotr in |g:crystalline_separators|.
  `left_group`
    The highlight group to the left.
  `right_group`
    The highlight group to the right.

Highlight groups must omit the "Crystalline" prefix.

See also |crystalline-highlight-groups|.

                                                   *crystalline.default_tab()*
crystalline#default_tab(buf, max_width, is_sel)    *crystalline#default_tab()*

The default tab/buffer.

See also |g:CrystallineTabFn|.

                                               *crystalline.tabs_or_buffers()*
crystalline#tabs_or_buffers(...)               *crystalline#tabs_or_buffers()*

Create a section with tabs or buffers.

Arguments:
  `opts`

  `opts.is_buffers`
    Display buffers instead of tabs.
    Defaults to false.

  `opts.enable_sep`
    Enable separators.
    Defaults to false.

  `opts.sep_index`
    The index of a separator in |g:crystalline_separators| to use.
    Defaults to 0.

  `opts.enable_mouse`
    Enable mouse controls. Not compatible with buffers.
    Defaults to true.

  `opts.auto_prefix_mode_group
    Whether to prefix the current mode to highlight groups.
    Defaults to |g:crystalline_auto_prefix_mode_group|.

  `opts.min_width`
    The minium visible width of the tabs/buffers.
    Takes precedence over `opts.max_width`.
    Defaults to 24.

  `opts.max_width`
    The maximum visible width of the tabs/buffers.
    This option should reflect available space.
    Defaults to the width of the screen.

  `opts.min_tab_width`
    The minimum width of individual tabs/buffers.
    Defaults to the space required for an empty buffer.

  `opts.min_tab_sel_width`
    The minimum width of the currently selected tab/buffer.
    Defaults to `opts.min_tab_width`.

  `opts.max_items`
    The maximum number of |'statusline'| or |'tabline'| items.
    This option should reflect the number of available items.

    Defaults to 80, which is the maximum number of items supported by VIM.

  `opts.group_suffix`
    A string to append to all group names.
    Defaults to |g:crystalline_group_suffix|.

  `opts.left_group`
    The group to the left of the tabs/buffers.
    A separator will be generated between the tabs/buffers and this section.

    To disable, set to an empty string.

    Defaults to "TabFill" if `sep_index` is left facing.

  `opts.right_group`
    The group to the right of the tabs/buffers.
    A separator will be generated between the tabs/buffers and this section.

    To disable, set to an empty string.

    Defaults to "TabFill" if `sep_index` is right facing.

See also |g:CrystallineTabFn|, |g:CrystallineHideBufferFn|.
See |crystalline-highlight-groups| for more info on groups.

                                                          *crystalline.tabs()*
crystalline#tabs(...)                                     *crystalline#tabs()*

Create a section with tabs.

See also |crystalline#tabs_or_buffers()|.

                                                       *crystalline.buffers()*
crystalline#buffers(...)                               *crystalline#buffers()*

Create a section with buffers.

See also |crystalline#tabs_or_buffers()|.

                                               *crystalline.default_tabline()*
crystalline#default_tabline(...)               *crystalline#default_tabline()*

Create a tabline with tabs or buffers.
Buffers are shown if only one tab is present. Otherwise, tabs are shown.

See also |crystalline#default_tabline_is_buffers()|,
|crystalline#tab_type_label()|, |crystalline#tabs_or_buffers()|.

                                    *crystalline.default_tabline_is_buffers()*
                                    *crystalline#default_tabline_is_buffers()*
crystalline#default_tabline_is_buffers()

Returns true when |crystalline#default_tabline()| shows buffers.

                                                *crystalline.tab_type_label()*
crystalline#tab_type_label(...)                 *crystalline#tab_type_label()*

Returns a string indicating if buffers or tabs are being shown.

Arguments:
  `is_buffers`
    Whether buffers are currently shown.

HIGHLIGHT GROUPS                                *crystalline-highlight-groups*
                                                          *crystalline-themes*

vim-crystalline themes typically provide these highlight groups.

Some themes which come with Crystalline automatically adjust to the
'background' with light/dark modes.

You do not have to use any of these groups, for example if you do not which to
display the current mode, use the default groups.

Example of how highlight groups might be used in your statusline/tabline:
>
    +--------------------------------------------------------+
    | TabType | Tab | TabSel | Tab |        TabFill          |
    |--------------------------------------------------------|
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |                                                        |
    |--------------------------------------------------------|
    | A | B |                Fill                    | B | A |
    +--------------------------------------------------------+
<

CrystallineA                                                    *CrystallineA*
CrystallineB                                                    *CrystallineB*

The default leftmost/rightmost and second leftmost/second rightmost statusline
sections.

Themes may provide more sections such as `CrystallineC`, `CrystallineD`, etc.

CrystallineFill                                              *CrystallineFill*

The middle statusline section.

CrystallineTab                                                *CrystallineTab*

Unselected tabs in the tabline.

CrystallineTabSel                                          *CrystallineTabSel*

Default selected tabs in the tabline.

CrystallineTabFill                                        *CrystallineTabFill*

The middle of the tabline.

CrystallineTabType                                        *CrystallineTabType*

The color of the tab type label on the tabline which reads "BUFFERS"/"TABS".

                                        *crystalline-highlight-group-variants*
CrystallineA1, CrystallineA2, CrystallineB1, CrystallineB2, etc.

Variants of the statusline and tabline groups.
Can be used to indicate different settings and states.

Themes may provide more variants such as 3, 4, 5, etc. or custom variants.

                                       *crystalline-inactive-highlight-groups*
CrystallineInactiveA, CrystallineInactiveA1, CrystallineInactiveB, etc.

Statusline and tabline groups in inactive windows.

                                              *crystalline-normal-mode-groups*
CrystallineNormalModeA, CrystallineNormalModeA1, CrystallineNormalModeB, etc.

Statusline and tabline groups in normal mode.

                                              *crystalline-command-mode-groups*
CrystallineCommandModeA, CrystallineCommandModeA1, etc.

Statusline and tabline groups in command mode.

                                              *crystalline-insert-mode-groups*
CrystallineInsertModeA, CrystallineInsertModeA1, CrystallineInsertModeB, etc.

Statusline and tabline groups in insert mode.

                                              *crystalline-visual-mode-groups*
CrystallineVisualModeA, CrystallineVisualModeA1, CrystallineVisualModeB, etc.

Statusline and tabline groups in visual mode.

                                              *crystalline-replace-mode-groups*
CrystallineReplaceModeA, CrystallineReplaceModeA1, etc.

Statusline and tabline groups in replace mode.

                                              *crystalline-terminal-mode-groups*
CrystallineTerminalModeA, CrystallineTerminalModeA1, etc.

Statusline and tabline groups in terminal mode.

CREATING THEMES                                  *crystalline-creating-themes*
                                                *crystalline.generate_theme()*
                                                *crystalline#generate_theme()*

Define a new theme by defining an |autoload| function which calls
`crystalline#generate_theme()`. Example:
>vim
    " ~/.vim/autoload/theme/my_theme.vim
    function! crystalline#theme#my_theme#set_theme() abort
      call crystalline#generate_theme({
            \ 'A':    [[17,  190], ['#00005f', '#dfff00'], 'cterm=bold gui=bold'],
            \ 'B':    [[255, 238], ['#ffffff', '#444444'], ''],
            \ 'Fill': [[85,  234], ['#9cffd3', '#202020'], ''],
            \ })
    endfunction
<

Each group is a list containing the following:
  `[[<ctermfg>, <ctermbg>], [<guifg>, <guibg>], <extra attributes>]`

Undefined groups/attributes will have the following defaults:

  `*Tab`       `CrystallineInactiveFill` or `CrystallineFill`.
  `*TabSel`    `*A`
  `*TabFill`   `*Fill`.
  `*TabType`   `*B`.

Where `*` is `Inactive`, `NormalMode`, `CommandMode`, etc.

Variants default to non-default variants.
Only variants 1-2 are supported by default.

At a minimum, define all attributes for `A`, `B`, and `Fill`.

For a full example of creating a custom theme, see the solarized theme:
<https://github.com/rbong/vim-crystalline/blob/master/autoload/crystalline/theme/solarized.vim>

See also |crystalline-highlight-groups|.

EXAMPLE                                                  *crystalline-example*

>vim
    function! g:CrystallineStatuslineFn(winnr)
      let l:curr = a:winnr == winnr()
      let l:s = ''

      if l:curr
        let l:s .= crystalline#mode_section(0, 'A', 'B')
      else
        let l:s .= crystalline#hi_item('InactiveFill')
      endif
      let l:s .= ' %f%h%w%m%r '
      if l:curr
        let l:s .= crystalline#sep(0, 'B', 'Fill') . ' %{fugitive#Head()}'
      endif

      let l:s .= '%='
      if l:curr
        let l:s .= crystalline#sep(1, 'Fill', 'B') . ' %{&paste ?"PASTE ":""}%{&spell?"SPELL ":""}'
        let l:s .= crystalline#sep(1, 'B', crystalline#mode_group('A'))
      endif
      if winwidth(a:winnr) > 80
        let l:s .= ' %{&ft}[%{&fenc!=#""?&fenc:&enc}][%{&ff}] %l/%L %c%V %P '
      else
        let l:s .= ' '
      endif

      return l:s
    endfunction

    function! g:CrystallineTablineFn()
      let l:vimlabel = has('nvim') ?  ' NVIM ' : ' VIM '
      return crystalline#default_tabline({
            \ 'enable_sep': 1,
            \ 'max_items': 80 - 2,
            \ 'max_width': &columns - strchars(l:vimlabel),
            \ })
            \ . '%=' . crystalline#hi_item('TabType') . ' ' . l:vimlabel
    endfunction

    let g:crystalline_theme = 'default'

    set showtabline=2
    set guioptions-=e
    set laststatus=2
<

See <https://github.com/rbong/vim-crystalline> for more examples.

 vim:tw=78:ts=8:ft=help
